buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "kotlin"

group = 'com.cable'
version = '1.0'
archivesBaseName = 'cable-libs'

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.google.inject:guice:4.1.0'
    compile 'com.google.inject.extensions:guice-assistedinject:4.1.0'
    compile 'com.google.guava:guava:21.0'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

shadowJar {
    classifier = null
    dependencies {
        include dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72")
        include dependency("com.google.inject:guice:4.1.0")
        include dependency("com.google.inject.extensions:guice-assistedinject:4.1.0")
    }
    relocate("org.jetbrains", "com.cable.library.shaded.jetbrains")
    relocate("kotlin", "com.cable.library.shaded.kotlin")
    relocate("com.google", "com.cable.library.shaded.google")
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
    task.options.fork = true
}
tasks.build.dependsOn reobfShadowJar

idea { module { inheritOutputDirs = true } }
